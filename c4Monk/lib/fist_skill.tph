

//// 拳法
OUTER_FOR (f=1;f<9;++f) BEGIN
	COPY ~%mod_root%/spl/base/c4mkf00.spl~ ~%workspace%/c4mkf%f%0.spl~
		PATCH_FOR_EACH lvl IN a b c d e BEGIN
			LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=321 timing=0 STR_VAR resource=~c4mkf%f%%lvl%~ END
		END
	LAF skill_creation STR_VAR spellname=~c4mkf%f%~ END
END


OUTER_FOR (i=1;i<6;++i) BEGIN
	OUTER_PATCH_SAVE level ~%i%~ BEGIN
		WRITE_BYTE 0 THIS+48
	END
	OUTER_SET percentage = i=1 ? 5 : i=2 ? 8 : i=3 ? 13 : i=4 ? 21 : 34
	OUTER_SET chance = 100 - percentage

	//ADDED 4.4
	OUTER_SET hpercentage = i=1 ? 8 : i=2 ? 11 : i=3 ? 14 : i=4 ? 17 : 100
	OUTER_SET hchance = 100 - hpercentage
	OUTER_SET htype = i=5 ? 1 : 0
	
	//冰
	COPY_EXISTING ~c4mkf1%level%.spl~ override
		PATCH_FOR_EACH effect IN 1 2 a BEGIN
			LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=248 STR_VAR resource=~c4mkf1%level%%effect%~ END
		END
		LPF ALTER_EFFECT INT_VAR match_opcode=278 parameter1=i END

	LAF CREATE_EFFECT INT_VAR opcode=40 duration=i*3+3 probability2=chance STR_VAR name=~c4mkf1%level%1~ END
	LAF CREATE_EFFECT INT_VAR opcode=139 timing=1 parameter1=14000 probability2=chance STR_VAR name=~c4mkf1%level%2~ END
	LAF CREATE_EFFECT INT_VAR opcode=12 timing=1 parameter2=2<<16 dicenumber=i dicesize=4 STR_VAR name=~c4mkf1%level%a~ END

	//火
	COPY_EXISTING ~c4mkf2%level%.spl~ override
		PATCH_FOR_EACH effect IN 1 BEGIN
			LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=248 STR_VAR resource=~c4mkf2%level%%effect%~ END
		END
		LPF ALTER_EFFECT INT_VAR match_opcode=278 parameter1=i END
		
	LAF CREATE_EFFECT INT_VAR opcode=146 timing=1 parameter2=1 STR_VAR name=~c4mkf2%level%1~ resource=~c4mkf2%level%1~ END	
	
	ADD_PROJECTILE  ~%mod_root%/pro/c4mkf2p.pro~
	COPY_EXISTING spcl819.spl ~override/c4mkf2%level%1.spl~
		LPF ALTER_SPELL_HEADER INT_VAR projectile=c4mkf2p END
		LPF ALTER_EFFECT INT_VAR opcode=30 timing=0 duration=12 END
		LPF CLONE_EFFECT INT_VAR opcode=12 timing=1 duration=0 parameter1=0 parameter2=8<<16 dicenumber=i dicesize=4 END
		GET_OFFSET_ARRAY header SPL_V10_HEADERS
		PHP_EACH header AS int => index BEGIN
			GET_OFFSET_ARRAY2 effect index SPL_V10_HEAD_EFFECTS
			PHP_EACH effect AS int => offset BEGIN
				READ_SHORT offset opcode
				PATCH_IF opcode=30 BEGIN
					WRITE_LONG offset+4 ~-5~
				END
			END
		END
		
	//电
	COPY_EXISTING ~c4mkf3%level%.spl~ override
		PATCH_FOR_EACH effect IN 1 2 a BEGIN
			LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=248 STR_VAR resource=~c4mkf3%level%%effect%~ END
		END
		LPF ALTER_EFFECT INT_VAR match_opcode=278 parameter1=i END

	LAF CREATE_EFFECT INT_VAR opcode=45 duration=i+1 probability2=chance STR_VAR name=~c4mkf3%level%1~ END
	LAF CREATE_EFFECT INT_VAR opcode=139 timing=1 parameter1=0x500 probability2=chance STR_VAR name=~c4mkf3%level%2~ END
	LAF CREATE_EFFECT INT_VAR opcode=12 timing=1 parameter2=4<<16 dicenumber=i dicesize=4 STR_VAR name=~c4mkf3%level%a~ END
	
	//酸 噬心
	COPY_EXISTING ~c4mkf4%level%.spl~ override
		PATCH_FOR_EACH effect IN 1 a BEGIN
			LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=248 STR_VAR resource=~c4mkf4%level%%effect%~ END
		END
		LPF ALTER_EFFECT INT_VAR match_opcode=278 parameter1=i END

	LAF CREATE_EFFECT INT_VAR opcode=0 timing=9 parameter1=~-1~ probability2=chance STR_VAR name=~c4mkf4%level%1~ END
	LAF CREATE_EFFECT INT_VAR opcode=12 timing=1 parameter2=1<<16 dicenumber=i dicesize=4 STR_VAR name=~c4mkf4%level%a~ END
	
	//金刚掌
	COPY_EXISTING ~c4mkf5%level%.spl~ override
		PATCH_IF i=5 THEN
		BEGIN
			LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=250 parameter1=25 parameter2=0 duration=1 probability2=85 END
			LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=12 timing=1 parameter2=1<<24 dicenumber=1 dicesize=10 probability2=85 END
		END
		PATCH_FOR_EACH effect IN 1 2 a BEGIN
			LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=248 STR_VAR resource=~c4mkf5%level%%effect%~ END
		END
		
		LPF ALTER_EFFECT INT_VAR match_opcode=278 parameter1=i END

	LAF CREATE_EFFECT INT_VAR opcode=93 parameter1=9 parameter2=1 timing=0 duration=18 probability2=chance STR_VAR name=~c4mkf5%level%1~ END
	LAF CREATE_EFFECT INT_VAR opcode=139 timing=1 parameter1=11330 probability2=chance STR_VAR name=~c4mkf5%level%2~ END
	LAF CREATE_EFFECT INT_VAR opcode=12 timing=1 parameter2=1<<24 dicenumber=i dicesize=3 STR_VAR name=~c4mkf5%level%a~ END

	//点穴手
	COPY_EXISTING ~c4mkf6%level%.spl~ override
		PATCH_IF i=5 THEN
		BEGIN
			LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=12 timing=4 duration=1 parameter1=2 parameter2=1<<20 dicenumber=0 dicesize=10 probability2=85 END
			LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=12 timing=4 duration=2 parameter1=2 parameter2=1<<20 dicenumber=0 dicesize=10 probability2=85 END
			LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=12 timing=4 duration=3 parameter1=2 parameter2=1<<20 dicenumber=0 dicesize=10 probability2=85 END
			LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=12 timing=4 duration=4 parameter1=2 parameter2=1<<20 dicenumber=0 dicesize=10 probability2=85 END
			LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=12 timing=4 duration=5 parameter1=2 parameter2=1<<20 dicenumber=0 dicesize=10 probability2=85 END
		END
		PATCH_FOR_EACH effect IN 1 a BEGIN
			LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=248 STR_VAR resource=~c4mkf6%level%%effect%~ END
		END
		
		LPF ALTER_EFFECT INT_VAR match_opcode=278 parameter1=i END

	LAF CREATE_EFFECT INT_VAR opcode=362 parameter1=1 parameter2=0 special=0 timing=0 duration=i*3+3 probability2=chance STR_VAR name=~c4mkf6%level%1~ END
	LAF CREATE_EFFECT INT_VAR opcode=12 timing=1 parameter2=1<<20 dicenumber=i dicesize=3 STR_VAR name=~c4mkf6%level%a~ END
	
	//螳螂拳
	COPY_EXISTING ~c4mkf7%level%.spl~ override
		PATCH_FOR_EACH effect IN 1 2 BEGIN
			LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=248 STR_VAR resource=~c4mkf7%level%%effect%~ END
		END
		
		LPF ALTER_EFFECT INT_VAR match_opcode=278 parameter1=i END

	ACTION_IF i=5 THEN BEGIN
		LAF CREATE_EFFECT INT_VAR opcode=317 parameter1=0 parameter2=1 timing=0 target=1 duration=90 probability2=hchance STR_VAR name=~c4mkf7%level%1~ END
		LAF CREATE_EFFECT INT_VAR opcode=139 timing=1 parameter1=1565 probability2=chance STR_VAR name=~c4mkf7%level%2~ END
	END
	ELSE BEGIN
		LAF CREATE_EFFECT INT_VAR opcode=317 parameter1=0 parameter2=0 timing=0 target=1 duration=90 probability2=hchance STR_VAR name=~c4mkf7%level%1~ END
		LAF CREATE_EFFECT INT_VAR opcode=139 timing=1 parameter1=349 probability2=chance STR_VAR name=~c4mkf7%level%2~ END
	END
	//LAF CREATE_EFFECT INT_VAR opcode=12 timing=1 parameter2=0 dicenumber=0 dicesize=3 STR_VAR name=~c4mkf7%level%a~ END

	//伤害惩罚
	COPY_EXISTING_REGEXP GLOB ~c4mkf[1-7]%level%.spl~ override
		decrease=i*~-2~
		GET_OFFSET_ARRAY header SPL_V10_HEADERS
		PHP_EACH header AS int => index BEGIN
			GET_OFFSET_ARRAY2 effect index SPL_V10_HEAD_EFFECTS
			PHP_EACH effect AS int => offset BEGIN
				READ_SHORT offset opcode
				PATCH_IF opcode=73 BEGIN
					WRITE_LONG offset+4 decrease
				END
			END
		END

END

