
//乱舞
COPY_EXISTING c4mkrage.spl override
	WRITE_ASCII 0x3a c4mkragb
	LPF ALTER_SPELL_HEADER STR_VAR icon=c4mkragb END
	SAY NAME1 @2001
	SAY UNIDENTIFIED_DESC @2002
	LPF CREATE_HEADERS_AS_LEVEL INT_VAR level_increment=30 level_multiplier=10 END
	PATCH_FOR_EACH opcode IN 142 174 317 BEGIN
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR opcode duration=36 multi_duration=18 END
	END	
	
//弱点
COPY_EXISTING c4mksnif.spl override
	WRITE_ASCII 0x3a c4mksnib
	LPF ALTER_SPELL_HEADER STR_VAR icon=c4mksnib END
	SAY NAME1 @2003
	SAY UNIDENTIFIED_DESC @2004
	PATCH_FOR_EACH dmgtype IN 1 2 3 4 7 8 BEGIN
		LPF CLONE_EFFECT INT_VAR match_opcode=215 opcode=332 parameter1=8 parameter2=dmgtype STR_VAR resource=~~ END
	END

//回溯	
COPY_EXISTING c4mkrevi.spl override
	WRITE_ASCII 0x3a c4mkrevb
	LPF ALTER_SPELL_HEADER STR_VAR icon=c4mkrevb END
	SAY NAME1 @2005
	SAY UNIDENTIFIED_DESC @2006
	LPF CLONE_EFFECT INT_VAR match_opcode=215 opcode=289 parameter1=3 parameter2=0 STR_VAR resource=~~ END
	
//额外技能点
COPY_EXISTING c4mkski2.spl override
	WRITE_ASCII 0x3a c4mkskib
	LPF ALTER_SPELL_HEADER STR_VAR icon=c4mkskib END
	SAY NAME1 @2009
	SAY UNIDENTIFIED_DESC @2010
	
LAF CREATE_EFFECT INT_VAR opcode=309 timing=1 parameter1=1 parameter2=1 STR_VAR name=c4mkski2 resource=~c4MonkSk~ resource2=~illPoint~ END

//裂空	
OUTER_FOR (i=1;i<6;++i) BEGIN	
	COPY ~%mod_root%/spl/base/c4mkc0.spl~ ~override/c4mkc%i%.spl~
		WRITE_ASCII 0x3a c4mkcatb
		LPF ALTER_SPELL_HEADER STR_VAR icon=c4mkcatb END
		LPF ALTER_EFFECT STR_VAR resource=~c4mkc%i%a~ END
		name=2010 + i*2 -1
		desc=name+1
		WRITE_LONG 8 RESOLVE_STR_REF ((AT ~%name%~))
		WRITE_LONG 0x50 RESOLVE_STR_REF ((AT ~%desc%~))
		PATCH_IF i>1 BEGIN
			FOR (j=1;j<i;++j) BEGIN
				LPF CLONE_EFFECT INT_VAR match_opcode=248 opcode=321 timing=0 STR_VAR resource=~c4mkc%j%~ END
			END
		END

	OUTER_FOR (j=1;j<5;++j) BEGIN
		OUTER_PATCH_SAVE charge ~%j%~ BEGIN
			WRITE_BYTE 0 THIS + 48
		END
		
		LAF CREATE_EFFECT INT_VAR opcode=146 target=2 duration=1 parameter2=1 STR_VAR name=~c4mkc%i%%charge%0~ resource=~c4mkc%i%%charge%0~ END
		
		LAF CREATE_EFFECT INT_VAR opcode=146 target=2 timing=1 parameter2=1 probability1=i*20 STR_VAR name=~c4mkc%i%%charge%~ resource=~c4mkc%i%%charge%~ END
		
		COPY ~%mod_root%/spl/base/c4mkc00.spl~ ~override/c4mkc%i%%charge%.spl~
			INNER_PATCH_SAVE active_next ~%j%~ BEGIN
				WRITE_BYTE 0 THIS + 49
			END
			LPF ALTER_EFFECT INT_VAR match_opcode=206 STR_VAR resource=~c4mkc%i%a~ END
			LPF ALTER_EFFECT INT_VAR match_opcode=272 STR_VAR resource=~c4mkc%i%%charge%0~ END
			LPF ALTER_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~c4mkc%i%%active_next%~ END
			FOR (k=1;k<j;++k) BEGIN 
				INNER_PATCH_SAVE previous ~%k%~ BEGIN
					WRITE_BYTE 0 THIS + 48
				END
				LPF CLONE_EFFECT INT_VAR match_opcode=248 opcode=321 timing=0 duration=0 STR_VAR resource=~c4mkc%i%%previous%~ END
			END
		
		COPY ~%mod_root%/spl/base/c4mkc000.spl~ ~override/c4mkc%i%%charge%0.spl~
			LPF ALTER_EFFECT STR_VAR resource=~c4mkc%charge%~ END
			FOR (k=1;k<j;++k) BEGIN 
				INNER_PATCH_SAVE previous ~%k%~ BEGIN
					WRITE_BYTE 0 THIS + 48
				END
				LPF CLONE_EFFECT INT_VAR match_opcode=215 opcode=321 timing=0 duration=0 parameter2=0 STR_VAR resource=~c4mkc%i%%previous%0~ END
			END
	END
		
	COPY_EXISTING ~c4mkc%i%d.spl~ override
		//去掉提气
		//LPF CLONE_EFFECT INT_VAR match_opcode=248 opcode=250 parameter1=25 parameter2=0 STR_VAR resource=~~ END
		//伤害
		LPF CLONE_EFFECT INT_VAR match_opcode=248 opcode=332 parameter1=50+i*10 parameter2=9 STR_VAR resource=~~ END
		PATCH_FOR_EACH dmgtype IN 1 2 3 4 7 8 BEGIN
			LPF CLONE_EFFECT INT_VAR match_opcode=248 opcode=332 parameter1=i*10 parameter2=dmgtype STR_VAR resource=~~ END
		END
		//终结技
		LPF ALTER_EFFECT STR_VAR match_resource=~c4mkc%i%e~ resource=~c4mkc%i%x~ END
	
		
	LAF CREATE_EFFECT INT_VAR opcode=146 target=2 duration=1 parameter2=1 STR_VAR name=~c4mkc%i%x~ resource=~c4mkc%i%x~ END
	
	COPY ~%mod_root%/spl/base/c4mkc000.spl~ ~override/c4mkc%i%x.spl~
		LPF ALTER_EFFECT STR_VAR resource=~c4mkce~ END
		LPF CLONE_EFFECT INT_VAR match_opcode=215 opcode=321 timing=0 duration=0 parameter2=0 STR_VAR resource=~c4mkc%i%d0~ END
		LPF CLONE_EFFECT INT_VAR match_opcode=215 opcode=321 timing=0 duration=0 parameter2=0 STR_VAR resource=~c4mkc%i%d~ END
		LPF CLONE_EFFECT INT_VAR match_opcode=215 opcode=139 timing=1 parameter1=RESOLVE_STR_REF (@2021) parameter2=0 STR_VAR resource=~c4mkc%i%d~ END

END
		
// test
// COPY_EXISTING_REGEXP GLOB ~c4mk[1-5][a-d].eff~ override
	// WRITE_SHORT 0x2c 100